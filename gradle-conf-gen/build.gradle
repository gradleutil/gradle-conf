plugins {
    id 'groovy'
    id 'maven-publish'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://jitpack.io" }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}

tasks.compileJava {
    options.release.set(8)
}

jar {
    def jtedir = layout.buildDirectory.dir('jteFiles')
    doFirst{
        net.gradleutil.gen.Generator.compile(file('src/main/jte').toPath(), jtedir.get().asFile.toPath())
    }
    from(jtedir){
        exclude "**/*.java"
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:' + config.versions.groovy
    implementation 'net.gradleutil:conf-gen:1.2.0'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

javadoc {
    failOnError = false
}

group = config.group
version = project.version != 'unspecified' ? project.version : file('VERSION').text.trim()

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

tasks.withType(AbstractPublishToMaven) { publishTask ->
    doLast {
        if (publishTask instanceof PublishToMavenRepository) {
            logger.lifecycle("Published ${project.group}.${project.name}:${version} to ${publishTask.repository.url}")
        } else if (publishTask instanceof PublishToMavenLocal) {
            String repoPath = repositories.mavenLocal().url.toURL().getFile()
            publication.with { p ->
                def sb = new StringBuilder()
                p.artifacts.each {
                    String artifactPath = p.groupId.replace(".", "/") + "/" + p.artifactId + "/" + p.version + "/" +
                            p.artifactId + "-" + p.version
                    sb.append('\n  ' + repoPath).append(artifactPath)
                            .append(it.classifier ? '-' + it.classifier : '').append('.' + it.extension)
                }
                logger.lifecycle("Published ${p.groupId}:${p.artifactId}:${p.version}${sb.toString()}")
            }
        } else {
            logger.lifecycle("Published ${project.group}:${project.name}:${version}")
        }
    }
}
